plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.6.0'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec.set("${projectDir}/openapi/individuals-api.yaml")
    outputDir.set(file("build/generated-src/openapi").absolutePath)
    apiPackage = "com.example.individuals-api.api.client"
    modelPackage = "com.example.individuals-api.api.dto"
    validateSpec = false

    configOptions = [
            library        : "spring-cloud",
            useFeignClients: "true",
            interfaceOnly  : "true",
            useJakartaEe   : "true"
    ]
}

compileJava.dependsOn 'openApiGenerate'
group = 'com.example'
version = '1.0.0'
description = 'individuals-api'

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated-src/openapi/src/main/java"
        }
    }
}

tasks.named('compileJava') {
    dependsOn('openApiGenerate')
}

ext {
    set('snakeyaml.version', '2.0')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

def loadEnvFile() {
    def envFile = file('.env')
    if (envFile.exists()) {
        envFile.eachLine { line ->
            if (line.contains('=')) {
                def (key, value) = line.split('=', 2)
                def trimmedKey = key.trim()
                def trimmedValue = value.trim()
                System.setProperty(trimmedKey, trimmedValue)
                println "$trimmedKey=$trimmedValue"
            }
        }
    }
}

loadEnvFile()

def nexusUrl = System.getenv("NEXUS_URL") ?: System.getProperty("NEXUS_URL")
def nexusUser = System.getenv("NEXUS_USERNAME") ?: System.getProperty("NEXUS_USERNAME")
def nexusPassword = System.getenv("NEXUS_PASSWORD") ?: System.getProperty("NEXUS_PASSWORD")

if (!nexusUrl || !nexusUser || !nexusPassword) {
    throw new GradleException(
            "NEXUS_URL, NEXUS_USERNAME, or NEXUS_PASSWORD not set. " +
                    "Please create a .env file with these properties or set environment variables."
    )
}

repositories {
    mavenCentral()
    maven {
        url = uri(nexusUrl)
        allowInsecureProtocol = true
        credentials {
            username = nexusUser
            password = nexusPassword
        }
    }
}

dependencies {
    // SECURITY
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // KEYCLOAK
    implementation 'org.keycloak:keycloak-admin-client:21.1.1'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'org.glassfish:jakarta.el:4.0.2'

    // OPEN API
    implementation "io.swagger.core.v3:swagger-annotations:2.2.21"
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:2.2.0"

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    implementation('org.springframework.boot:spring-boot-starter-actuator')

    //Метрика
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation "net.logstash.logback:logstash-logback-encoder:7.4"
    implementation "io.opentelemetry:opentelemetry-exporter-otlp"
    implementation "io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter:2.6.0"
}

tasks.named('test') {
    useJUnitPlatform()
}